# This file contains the configuration of PATH variable.
#
# This file is sourced on all invocations of the shell, unless the
# `-f' option is set.
#   .zshenv -> [paths] -> .zprofile -> .zshrc -> .zlogin

# No duplicate entries are needed.
typeset -U path

path=(/usr/local/bin /usr/bin /bin /usr/sbin /sbin $path)

# Homebrew and sistemwide local binaries
[ -d /opt/homebrew/sbin ] && path=(/opt/homebrew/sbin $path)
[ -d /usr/local/sbin ] && path=(/usr/local/sbin $path)
[ -d /usr/local/bin ] && path=(/usr/local/bin $path)
[ -d /opt/homebrew/bin ] && path=(/opt/homebrew/bin $path)

# Include local bin
[ -e $HOME/bin ] && {
  [ -L $HOME/bin ] || [ -d $HOME/bin ] && {
    path=($HOME/bin $path)
  }
}

# Cargo binaries
[ -d $HOME/.cargo/bin ] && path=($HOME/.cargo/bin $path)

# Cabal
[ -d $HOME/.cabal/bin ] && path=($HOME/.cabal/bin $path)

# Cask
[ -d $HOME/.cask/bin ] && path=($HOME/.cask/bin $path)

# Go lang local workspace
if [ -d $HOME/go ]; then
  GOPATH=$HOME/go
  export GOPATH

  [ -d $GOPATH/bin ] && {
    # Put binary files created using "go install" command
    # in "$GOPATH/bin"
    GOBIN=$GOPATH/bin
    export GOBIN
    path=($GOBIN $path)
  }
fi

# Local binaries
[ -d $HOME/.local/bin ] && path=($HOME/.local/bin $path)

# OS specific paths.
[ -r $ZSHDDIR/conf.d/OS/$OSSHORT/paths ] && {
  source $ZSHDDIR/conf.d/OS/$OSSHORT/paths
}

export PATH

# ~/.zshenv is supposed to be the ideal place to set the PATH.
# However on OS X and Arch (and previously Gentoo), the system
# /etc/zprofile which is executed after ~/.zshenv will overwrite
# the PATH variable - so to workaround that issue, PATH settings
# should be re-read in ~/.zprofile
if [ ! -d ${XDG_CACHE_HOME:-$HOME/.cache}/zsh ]; then
  mkdir -p ${XDG_CACHE_HOME:-$HOME/.cache}/zsh
fi

if command -v hostname >/dev/null 2>&1; then
  ZSH_PATH_CACHE="${XDG_CACHE_HOME:-$HOME/.cache}/zsh/$(hostname -s).paths"
else
  ZSH_PATH_CACHE=${XDG_CACHE_HOME:-$HOME/.cache}/zsh/localhost.paths
fi

if touch $ZSH_PATH_CACHE >/dev/null 2>&1; then
  chmod 600 $ZSH_PATH_CACHE
  cat <<EOF > $ZSH_PATH_CACHE
export PATH="$PATH"
EOF
else
  >&2 echo -n "zenv: Unable to create PATH cache file: \"$ZSH_PATH_CACHE\""
fi
