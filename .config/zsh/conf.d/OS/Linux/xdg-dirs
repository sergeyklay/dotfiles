# Linux-wide Zsh environment configuration.
#
# This file is sourced on all invokations of zsh.
#   .zshenv -> [xdg-dirs] -> .zprofile -> .zshrc -> .zlogin
#                ^
#   -------------

# XDG Base Directory Configuration

# User-specific configuration files.
[ -z "$XDG_CONFIG_HOME" ] && export XDG_CONFIG_HOME="$HOME/.config"

# Configuration files should be searched relative to this directory.
[ -z "$XDG_CONFIG_DIRS" ] && export XDG_CONFIG_DIRS=/etc/xdg

# User-specific cached data should be written relative to this
# directory.
[ -z "$XDG_CACHE_HOME" ] && export XDG_CACHE_HOME="$HOME/.cache"

# User-specific data files.
[ -z "$XDG_DATA_HOME" ] && export XDG_DATA_HOME="$HOME/.local/share"

# User-specific runtime files should be placed relative to this
# directory.
[ -z "$XDG_RUNTIME_DIR" ] && {
  [ -d /run/user ] && export XDG_RUNTIME_DIR="/run/user/$(id -u)"
}

# See: https://stackoverflow.com/a/27965014/1661465
[ -z "$XDG_STATE_HOME" ] && {
  export XDG_STATE_HOME="$HOME/.local/state"
}
[ ! -d "$XDG_STATE_HOME" ] && mkdir -p "$XDG_STATE_HOME"

# Base directories relative to which data files should be searched.
typeset -U xdg_data_dirs
if [[ -n "$XDG_DATA_DIRS" ]]; then
  # Trim leading slashes to avoid array like
  #     (/foo/bar /foo/bar/)
  sub="/:"
  XDG_DATA_DIRS="${XDG_DATA_DIRS//$sub/:}"
  XDG_DATA_DIRS=${XDG_DATA_DIRS%/}
  unset sub

  xdg_data_dirs=("${(s.:.)XDG_DATA_DIRS}")
  xdg_data_dirs=(/usr/local/share /usr/share "$xdg_data_dirs[@]")
else
  xdg_data_dirs=(/usr/local/share /usr/share)
fi

if whence flatpak >/dev/null 2>&1; then
  [ -d /var/lib/flatpak/exports/share ] && {
    xdg_data_dirs+=/var/lib/flatpak/exports/share
  }

  [ -d $XDG_DATA_HOME/flatpak/exports/share ] && {
    xdg_data_dirs+=$XDG_DATA_HOME/flatpak/exports/share
  }
fi

export XDG_DATA_DIRS=${(j|:|)xdg_data_dirs}
unset xdg_data_dirs

if test -r "$XDG_CONFIG_HOME/user-dirs.dirs"
then
  source "$XDG_CONFIG_HOME/user-dirs.dirs"

  test -n "$XDG_DESKTOP_DIR" && export XDG_DESKTOP_DIR
  test -n "$XDG_DOCUMENTS_DIR" && export XDG_DOCUMENTS_DIR
  test -n "$XDG_DOWNLOAD_DIR" && export XDG_DOWNLOAD_DIR
  test -n "$XDG_MUSIC_DIR" && export XDG_MUSIC_DIR
  test -n "$XDG_PICTURES_DIR" && export XDG_PICTURES_DIR
  test -n "$XDG_PUBLICSHARE_DIR" && export XDG_PUBLICSHARE_DIR
  test -n "$XDG_VIDEOS_DIR" && export XDG_VIDEOS_DIR
fi

# Local Variables:
# mode: sh
# End:
